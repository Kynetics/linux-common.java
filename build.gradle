plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}




def globalName    = "linux-common"

def globalVersion = "0.1.1"

def globalDesc    = "Miscellaneous JNA utilities for Linux"

def globalUrl     = "https://github.com/helins/linux-common.java"




group   = "io.helins"
version = globalVersion



repositories {
    mavenCentral()
}



dependencies {
    api                "net.java.dev.jna:jna:5.7.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
    testRuntimeOnly    "org.junit.jupiter:junit-jupiter-engine:5.7.1"
}



java {
    withJavadocJar()
    withSourcesJar()
}



javadoc {
    options.links( "https://docs.oracle.com/javase/8/docs/api/"             ,
                   "http://java-native-access.github.io/jna/4.5.1/javadoc/" )
}



test {

    useJUnitPlatform()

    testLogging {

        events "passed"  ,
               "skipped" ,
               "failed"
    }
}




// Publishing to Maven


publishing {


    publications {
        LibMaven( MavenPublication ) {

            from components.java


            pom {


                name        = globalName
                description = globalDesc
                url         = globalUrl

                licenses {
                     license {
                         name = "Mozilla Public License Version 2.0"
                         url  = "https://www.mozilla.org/en-US/MPL/2.0/"
                     }
                }


                developers {
                    developer {
                        name            = "Adam Helinski"
                        email           = "adam@helins.io"
                        organizationUrl = "http://www.helins.io"
                    }
                }
            
            
                scm {
                    connection          = "scm:git:git://github.com/helins/linux-common.java.git"
                    developerConnection = "scm:git:ssh://github.com/helins/linux-common.java.git"
                    url                 = globalUrl
                }
            }
        }
    }


    repositories {
        maven {
            credentials( PasswordCredentials )
            name = "sonatype"
            url  = "https://s01.oss.sonatype.org"
        }
    }
}




signing {
    sign publishing.publications.LibMaven
}
